use std::str::FromStr;
use crate::{ Sexpr};
use crate::source_location::SourceLocation;

grammar<'a>(context: &'a SourceLocation<()>);

pub Datum: SourceLocation<Sexpr> = {
    <l:@L> <x:RawSexpr> <r:@R> => context.clone().with_span(l..r).map_value(x),
};

pub RawSexpr: Sexpr = {
    <b:Bool> => Sexpr::bool(b),
    <n:Num> => Sexpr::int(n),
    <s:Sym> => Sexpr::symbol(s),
    <s:Str> => Sexpr::string(s),
    <p:Pair> => Sexpr::cons(p.0, p.1),
    <l:List> => <>,
    <q:Quotation> => <>,
};

Bool: bool = {
    "#t" => true,
    "#f" => false,
}

Num: i64 = <s:r"[0-9]+"> => i64::from_str(s).unwrap();

Sym: &'input str = <s:r"[a-zA-Z:!?-]([a-zA-Z0-9:!?-])*"> => s;

Str: &'input str = {
    <s:r#""[^"]*""#> => &s[1..s.len()-1],
 };

Pair: (SourceLocation<Sexpr>, SourceLocation<Sexpr>) = {
    "(" <car:Datum> "." <cdr:Datum> ")" => (car, cdr)
};

List: Sexpr = {
    "(" ")" => Sexpr::nil(),
    "(" <Sequence> ")" => <>,
};

Sequence: Sexpr = {
    <car:Datum> <l:@L> <cdr:Sequence> <r:@R> => Sexpr::cons(car, context.clone().with_span(l..r).map_value(cdr)),
    <car:Datum> => Sexpr::cons(car, Sexpr::nil()),
};

Quotation: Sexpr = {
    <l:@L> "'" <r:@R> <x:Datum> => Sexpr::list(vec![context.clone().with_span(l..r).map_value(Sexpr::symbol("quote")), x].into_iter())
};
