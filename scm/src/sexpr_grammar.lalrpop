use lalrpop_util::ParseError;
use std::borrow::Cow;
use std::collections::HashMap;
use std::str::FromStr;
use crate::parser::unescape;
use crate::Scm;
use crate::source_location::SourceLocation;
use crate::source_map::SourceMap;
use crate::scm::number::Number;

grammar<'a>(context: &'a SourceLocation<()>, source_map: &SourceMap, datum_labels: &mut HashMap<&'input str, Scm>);

match {
    r"\s*" => { }, // The default whitespace skipping is disabled an `ignore pattern` is specified
    r";[^\n\r]*[\n\r]*" => LINE_COMMENT,
    r"#\|[^|]*\|+([^|#][^|]*\|+)*#" => BLOCK_COMMENT,
    ".",
    r"\|(\\\||[^|])*\|" => VERBATIM_SYMBOL,
    r"[a-zA-Z!$%&*+./:<=>?@^_~-]([^\s\(\)])*" => IDENTIFIER,
} else {
    _
}

pub ExplicitSequence: Vec<Scm> = {
    <cd:OptionalDatum*> => cd.into_iter().filter_map(|x|x).collect(),
};

OptionalDatum: Option<Scm> = {
    Comment => None,
    <d:Datum> => Some(d),
};

Comment: () = {
    "#;" Datum,
    LINE_COMMENT,
    BLOCK_COMMENT,
};

Datum: Scm = {
    <i:r"#([0-9]+)="> <d:Datum> => { datum_labels.insert(&i[1..i.len()-1], d.clone()); d },
    <l:@L> <i:r"#([0-9]+)#"> <r:@R> =>? {
        let src = context.clone().with_span(l..r);
        let sexpr = datum_labels
            .get(&i[1..i.len()-1])
            .cloned()
            .ok_or(ParseError::User { error: "undefined datum label" })?;
        source_map.insert(sexpr.clone(), src);
        Ok(sexpr)
    },
    <l:@L> <x:RawSexpr> <r:@R> => {
        let src = context.clone().with_span(l..r);
        source_map.insert(x.clone(), src);
        x
    },
};

RawSexpr: Scm = {
    <b:Bool> => Scm::bool(b),
    <n:Num> => Scm::number(n),
    <ch:Char> => Scm::char(ch),
    <s:Sym> => Scm::symbol(&s),
    <s:Str> => Scm::string(s),
    <bv:ByteVec> => Scm::bytevector(bv),
    <v:Vec> => Scm::vector(v),
    <l:List> => <>,
    <q:Quotation> => <>,
};

Bool: bool = {
    "#t" => true,
    "#f" => false,
}

Num: Number = {
    <s:r"[0-9]+"> => Number::int(i64::from_str(s).unwrap()),
    <s:r"[0-9]+\.[0-9]*"> => Number::float(f64::from_str(s).unwrap()),
}

Char: char = {
    <s:r"#\\[xX][0-9A-Fa-f]+"> =>? u32::from_str_radix(&s[3..], 16)
                                    .ok()
                                    .and_then(std::char::from_u32)
                                    .ok_or(ParseError::User { error: "invalid unicode character" }),
    <l:@L><s:r"#\\[xX][^0-9A-Fa-f]+"><r:@R> =>? match &s[2..] {
                                    "xi" => Ok('ξ'),       "Xi" => Ok('Ξ'),
                                    _ => Err(ParseError::User { error: "invalid named character" })
                                },
    <l:@L><s:r"#\\[^xX][a-zA-Z]+"><r:@R> =>? match &s[2..] {
                                    "alarm" => Ok('\u{0007}'),
                                    "backspace" => Ok('\u{0008}'),
                                    "delete" => Ok('\u{007f}'),
                                    "escape" => Ok('\u{001b}'),
                                    "newline" => Ok('\n'),
                                    "null" => Ok('\u{0000}'),
                                    "return" => Ok('\r'),
                                    "space" => Ok(' '),
                                    "tab" => Ok('\t'),

                                    "alpha" => Ok('α'),    "Alpha" => Ok('Α'),
                                    "beta" => Ok('β'),     "Beta" => Ok('Β'),
                                    "gamma" => Ok('γ'),    "Gamma" => Ok('Γ'),
                                    "delta" => Ok('δ'),    "Delta" => Ok('Δ'),
                                    "epsilon" => Ok('ε'),  "Epsilon" => Ok('Ε'),
                                    "zeta" => Ok('ζ'),     "Zeta" => Ok('Ζ'),
                                    "eta" => Ok('η'),      "Eta" => Ok('Η'),
                                    "theta" => Ok('θ'),    "Theta" => Ok('Θ'),
                                    "iota" => Ok('ι'),     "Iota" => Ok('Ι'),
                                    "kappa" => Ok('κ'),    "Kappa" => Ok('Κ'),
                                    "lambda" => Ok('λ'),   "Lambda" => Ok('Λ'),
                                    "mu" => Ok('μ'),       "Mu" => Ok('Μ'),
                                    "nu" => Ok('ν'),       "Nu" => Ok('Ν'),
                                    // xi is treated specially because of the leading x
                                    "omicron" => Ok('ο'),  "Omicron" => Ok('Ο'),
                                    "pi" => Ok('π'),       "Pi" => Ok('Π'),
                                    "rho" => Ok('ρ'),      "Rho" => Ok('Ρ'),
                                    "2sigma" => Ok('ς'),
                                    "sigma" => Ok('σ'),    "Sigma" => Ok('Σ'),
                                    "tau" => Ok('τ'),      "Tau" => Ok('Τ'),
                                    "upsilon" => Ok('υ'),  "Upsilon" => Ok('Υ'),
                                    "phi" => Ok('φ'),      "Phi" => Ok('Φ'),
                                    "chi" => Ok('χ'),      "Chi" => Ok('Χ'),
                                    "psi" => Ok('ψ'),      "Psi" => Ok('Ψ'),
                                    "omega" => Ok('ω'),    "Omega" => Ok('Ω'),

                                    _ => Err(ParseError::User { error: "invalid named character" })
                                },
    <s:r"#\\."> => s[2..].chars().next().unwrap(),
};

Sym: Cow<'input, str> = {
    <s:IDENTIFIER> => Cow::Borrowed(s),
    <s:VERBATIM_SYMBOL> =>? unescape(&s[1..s.len()-1]),
}

Str: &'input str = {
    <s:r#""[^"]*""#> => &s[1..s.len()-1],
};

List: Scm = {
    "(" ")" => Scm::null(),
    "(" <Sequence> ")" => <>,
};

Sequence: Scm = {
    <car:Comment> <seq:Sequence> => seq,
    <car:Datum> <l:@L> <cdr:Sequence> <r:@R> => {
        let src = context.clone().with_span(l..r);
        source_map.insert(cdr.clone(), src);
        Scm::cons(car, cdr)
    },
    <car:Datum> "." <cdr:Datum> => Scm::cons(car, cdr),
    <car:Datum> => Scm::cons(car, Scm::null()),
};

Quotation: Scm = {
    <l:@L> "'" <r:@R> <x:Datum> => {
        let quote = Scm::symbol("quote");
        let src = context.clone().with_span(l..r);
        source_map.insert(quote.clone(), src);
        Scm::list(vec![quote, x].into_iter())
    }
};

Vec: Vec<Scm> = {
    "#(" ")" => vec![],
    "#(" <VecSequence> ")" => <>,
}

VecSequence: Vec<Scm> = {
    <singleton:Datum> => vec![singleton],
    <mut head:VecSequence> <last:Datum> => { head.push(last); head },
};

ByteVec: Vec<u8> = {
    "#u8(" ")" => vec![],
    "#u8(" <ByteSequence> ")" => <>,
}

ByteSequence: Vec<u8> = {
    <singleton:ByteNum> => vec![singleton],
    <mut head:ByteSequence> <last:ByteNum> => { head.push(last); head },
}

ByteNum: u8 = {
    r"[0-9]+" =>? u8::from_str(<>).map_err(|_|ParseError::User { error: "invalid byte constant" })
}
