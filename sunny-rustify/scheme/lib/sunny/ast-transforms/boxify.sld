(define-library (sunny ast-transforms boxify)
  (export boxify)

  (import (scheme base)
          (sunny ast)
          (sunny variable))

  (begin
    (define (boxify node)
      (define (transform node ignore)
        (cond ((eq? (node 'kind) 'ABSTRACTION)
               (boxify-abstraction (node 'get-params)
                                   (node 'get-vars)
                                   (node 'get-params)
                                   (node 'get-vars)
                                   (node 'get-body)))
              ((eq? (node 'kind) 'VARARG-ABSTRACTION)
               (boxify-vararg-abstraction (node 'get-params)
                                          (node 'get-vararg)
                                          (node 'get-vars)
                                          (node 'get-varvar)
                                          (cons (node 'get-vararg)
                                                (node 'get-params))
                                          (cons (node 'get-varvar)
                                                (node 'get-vars))
                                          (node 'get-body)))
              ((eq? (node 'kind) 'FIXLET)
               (boxify-fixlet (node 'get-params)
                              (node 'get-vars)
                              (node 'get-args)
                              (node 'get-body)))
              (else (ignore))))
      (node 'transform transform))


    (define (boxify-abstraction params vars param* var* body)
      (if (null? var*)
          (make-abstraction params vars (boxify body))
          (if (variable-mutable? (car var*))
              (begin (local-boxify! (car var*))
                     (boxify-abstraction params vars (cdr param*) (cdr var*)
                                         (make-boxify (car param*) body)))
              (boxify-abstraction params vars (cdr param*) (cdr var*) body))))

    (define (boxify-vararg-abstraction params vararg vars varvar param* var* body)
      (if (null? var*)
          (make-vararg-abstraction params vararg vars varvar (boxify body))
          (if (variable-mutable? (car var*))
              (begin (local-boxify! (car var*))
                     (boxify-vararg-abstraction params vararg vars varvar
                                                (cdr param*) (cdr var*)
                                                (make-boxify (car param*) body)))
              (boxify-vararg-abstraction params vararg vars varvar (cdr param*) (cdr var*) body))))

    (define (boxify-fixlet params vars args body)
      (make-fixlet params vars args
        (boxify-vars! params vars (boxify body))))

    (define (boxify-vars! param* var* body)
      (cond ((null? var*)
             body)
            ((variable-mutable? (car var*))
             (local-boxify! (car var*))
             (boxify-vars! (cdr param*)
                           (cdr var*)
                           (make-boxify (car param*)
                                        body)))
            (else (boxify-vars! (cdr param*) (car var*) body))))))
